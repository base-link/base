
load @drumwork/dock/code/javascript/number
  take task add
  take task subtract
  take task decrement
load @drumwork/dock/code/javascript/base
  take task check-if-strictly-equal
  take task check-gt
  take task check-else
  take task walk
  take task set-field
  take task shift-left
load @drumwork/dock/code/javascript/console
  take task log
load @drumwork/dock/code/javascript/object
  take task create
    save task create-object
load @drumwork/dock/code/javascript/error
  take task create
    save task create-error
load @drumwork/base/code/dock/node/file
  take task read
    save task read-file
load @drumwork/base/code/host/form
  load /bind
    take form bind
  load /term
    take form term
  load /sift
    take form sift
  load /call
    take form call
  load /task
    take form task
  load /link
    take form link

task assert-equal
  base base
  base head

  call check-if-strictly-equal
    bind left, link base
    bind right, link head

task find-fibonacci-via-loop
  base i

  host g, size 0
  host o, size 1
  host d

  call walk
    hook test
      call check-gt
        bind left, link i
        bind right, text 0
    hook link
      save d, link o
      save o
        call add
          bind base, link g
          bind head, link d
      save g, link d
      save i
        call decrement
          bind integer, link i

  turn seed, link g

task find-fibonacci-via-recursion
  base i
  base g, size 0
  base o, size 1

  call check-else
    hook test
      call check-if-strictly-equal
        bind left, link i
        bind right, text 0
    hook like
      turn seed, link g
    hook miss
      save d
        call subtract
          bind base, link i
          bind head, text 1
      save t
        call add
          bind base, link o
          bind head, link g
      call find-fibonacci-via-recursion
        bind i, link d
        bind g, link o
        bind o, link t

task test-fibonacci-loop
  wait bind

  save o
    call find-fibonacci-via-loop
      bind i, text 7
  call log
    bind message, link o
  save x
    call find-fibonacci-via-recursion
      bind i, text 7
  call log
    bind message, link x
  save x
    call create-object
  call set-field
    bind object, link x
    bind attribute, text <foo>
    bind value, text <bar>
  call set-field
    bind object, link x
    bind attribute, text <x>
    bind value, link o
  save v, text <hello>
  save x/y
    call shift-left
      bind left, link o
      bind right, size 5
  call log
    bind message, link x
  save content
    call read-file
      bind path, text <deck.link>
      bind encoding, text <utf-8>
      wait bind
  call log
    bind message, link content
