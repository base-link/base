
load @drumwork/dock/code/javascript/number
  take task add
  take task subtract
  take task decrement
load @drumwork/dock/code/javascript/base
  take task check-if-strictly-equal
  take task check-gt
  take task check-else
  take task walk
  take task set-field
  take task shift-left
load @drumwork/dock/code/javascript/console
  take task log
load @drumwork/dock/code/javascript/object
  take task create
    save task create-object
load @drumwork/dock/code/javascript/error
  take task create
    save task create-error
load @drumwork/base/code/dock/node/file
  take task read
    save task read-file
load @drumwork/base/code/host/form
  load /bind
    take form bind
  load /term
    take form term
  load /sift
    take form sift
  load /call
    take form call
  load /task
    take form task
  load /link
    take form link

task assert-equal
  base base
  base head

  call check-if-strictly-equal
    bind left, link base
    bind right, link head

task find-fibonacci-via-loop
  base i

  host g, size 0
  host o, size 1
  host d

  call walk
    hook test
      call check-gt
        bind left, link i
        bind right, text 0
        turn seed
    hook link
      save d, link o
      call add
        bind base, link g
        bind head, link d
        save o
      save g, link d
      call decrement
        bind integer, link i
        save i

  turn seed, link g

task find-fibonacci-via-recursion
  base i
  base g, size 0
  base o, size 1

  call check-else
    hook test
      call check-if-strictly-equal
        bind left, link i
        bind right, text 0
        turn seed
    hook like
      turn seed, link g
    hook miss
      call subtract
        bind base, link i
        bind head, text 1
        save d
      call add
        bind base, link o
        bind head, link g
        save t
      call find-fibonacci-via-recursion
        bind i, link d
        bind g, link o
        bind o, link t
        turn seed
    turn seed

task test-fibonacci-loop
  wait bind

  call find-fibonacci-via-loop
    bind i, text 7
    save o
  call log
    bind message, link o
  call find-fibonacci-via-recursion
    bind i, text 7
    save x
  call log
    bind message, link x
  call create-object
    save x
  call set-field
    bind object, link x
    bind attribute, text <foo>
    bind value, text <bar>
  call set-field
    bind object, link x
    bind attribute, text <x>
    bind value, link o
  save v, text <hello>
  call shift-left
    bind left, link o
    bind right, size 5
    save x/y
  call log
    bind message, link x
  call read-file
    bind path, text <deck.link>
    bind encoding, text <utf-8>
    wait bind
    save content
  call log
    bind message, link content
